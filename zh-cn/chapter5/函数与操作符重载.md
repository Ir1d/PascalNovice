#函数与操作符重载

函数与过程类似，但区别在函数可以有返回值。定义函数的格式如下：

```
function <函数名>(<参数变量名> :<数据类型>, <参数变量名> :<数据类型>, ...) :<数据类型>;
begin
end;
```

其中，最后一个<数据类型>是指函数返回值的数据类型，例如，给一个简单的“二次函数”写一个函数：


```delphi
//程序5_2_1_function
function f(x :extended) :extended;
begin
	f := sqr(x) + x + 1;
end;
begin
	writeln(f(1));
	writeln(f(-1.5));
end.
```

于是，在上一节反复提到的“判断质数”也可以这样写：

```delphi
//程序5_2_2_prime_function
function isprime(x :longint) :boolean;
var i :longint;
begin
	for i := 2 to trunc(sqrt(x)) do if x mod i = 0 then begin
		isprime := false;
		exit;
	end;
	isprime := true;
end;
procedure main;
var n, a, i :longint;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		if isprime(a) then writeln('YES') else writeln('NO');
	end;
end;
begin
	main;
end.
```

在函数当中，exit语句可以带参数，表明直接返回参数的值。例如，以上程序也可以这样改写：

```delphi
//程序5_2_3_prime_function_exit
function isprime(x :longint) :boolean;
var i :longint;
begin
	for i := 2 to trunc(sqrt(x)) do if x mod i = 0 then exit(false);
	exit(true);
end;
procedure main;
var n, a, i :longint;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		if isprime(a) then writeln('YES') else writeln('NO');
	end;
end;
begin
	main;
end.
```

下面是另一个问题：操作符重载。记录类型不能直接比较大小（编译错误），而是需要我们定义它。

```
operator <操作符>(<参数名1>, <参数名2> :<数据类型>) <结果变量名> :<数据类型>;
```

其中<操作符>通常为<，<=，>，>=，<>，=，+，-，*，/。对于前几种比较操作符，结果变量的数据类型通常为boolean，对于其余的操作符，结果变量的数据类型通常与原数据类型相同。

例如：

```delphi
//程序5_2_4_sc
type human = record
	name :string;
	age :longint;
end;
function older(x, y :human) :boolean;
begin
	older := x.age > y.age;
end;
operator >(x, y :human) z :boolean;
begin
	z := x.age > y.age;
end;
var xiaoming, xiaohong :human;
begin
	xiaoming.name := 'xiaoming';
	xiaoming.age := 12;
	xiaohong.name := 'xiaohong';
	xiaohong.age := 11;
	if xiaoming > xiaohong then
		//与xiaoming.age > xiaohong.age等效
		//与older(xiaoming, xiaohong)等效
	writeln('Older') else writeln('NotOlder');
end.
```

再例如，一些形如$$a+b*sqrt(2)$$（a与b为有理数）的数，可以这样表示：

```
//程序5_2_5_num
type num = record
	a :extended;
	b :extended;
end;
operator +(x, y :num) z :num;
begin
	z.a := x.a + y.a;
	z.b := x.b + y.b;
end;
operator –(x, y :num) z :num;
begin
	z.a := x.a – y.a;
	z.b := x.b – y.b;
end;
operator *(x, y :num) z :num;
begin
	z.a := x.a * y.a + 2 * x.b * y.b;
	z.b := x.b * y.a + x.a * y.b;
end;
operator /(x, y :num) z :num;
begin
	z.a := (x.a * y.a - 2 * x.b * y.b) / (sqr(y.a) - 2 * sqr(y.b));
	z.b := x.b * y.a - x.a * y.b;
end;
begin
end.
```

---------

gl & hf;

开始新的OI征程吧～
