#过程

输入n和n个数，依次判断它们是否是质数：

```delphi
//程序5_1_1_prime
var n, a, i, j :longint;
var isPrime :boolean;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		isPrime := true;
		for j := 2 to trunc(sqrt(a)) do if a mod j = 0 then begin
			isPrime := false;
			break;
		end;
		if isPrime then writeln('YES') else writeln('NO');
	end;
end.
```

我们发现，“判断一个数是否是质数”是一个通用算法，我们想把它单独拿出来作为一个“小部分”，也就是过程。

过程的定义如下：

```
procedure <过程名>;
(在此处定义过程中需要的各种变量)
var ....
begin
	<语句>;
	<语句>;
	...
end;
```

这样以后，过程就可以当成一个普通的语句是使用了。

过程与变量的定义位置相同，同理，过程名不能是保留字，也不能是与变量重名。

过程内部的变量只在过程里有效，且如果与全局变量重名，则在过程中，**全局变量会被忽略。**

例如，以下程序与上述程序的功能完全相同：

```delphi
//程序5_1_2_prime_procedure
var n, a, i :longint;
procedure prime;
var isPrime :boolean;
var i :longint;
begin
	isPrime := true;
	for i := 2 to trunc(sqrt(a)) do if a mod i = 0 then begin
		isPrime := false;
		break;
	end;
	if isPrime then writeln('YES') else writeln('NO');
end;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		prime;
	end;
end.
```

利用exit语句可以直接跳出当前一层过程，例如，上述程序也可以这样改写：

```
//程序5_1_3_prime_exit
var n, a, i :longint;
procedure prime;
var i :longint;
begin
	for i := 2 to trunc(sqrt(a)) do if a mod i = 0 then begin
		writeln('NO');
		exit;
	end;
	writeln('YES');
end;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		prime;
	end;
end.
```

而利用halt语句可以在任何地方直接结束程序，但是务必注意：如果程序是写了文件的，请务必在调用halt之前先close掉相关文件。

我们可以向一个过程传递参数，格式如下：

```
procedure <过程名>(<参数变量名> :<数据类型>, <参数变量名> :<数据类型>, ...);
begin
	<语句>;
	<语句>;
	...
end;
```

例如，下面的程序与以上的功能又完全相同：

```delphi
//程序5_1_4_prime_parameter
procedure prime(x :longint);
var i :longint;
begin
	for i := 2 to trunc(sqrt(x)) doif x mod i = 0 then begin
		writeln('NO');
		exit;
	end;
	writeln('YES');
end;
```

```delphi
procedure main; //把主程序定义成一个过程也是一个好方法
var n, a, i :longint;
begin
	read(n);
	for i := 1 to n do begin
		read(a);
		prime(a);
	end;
end;
begin
	main;
end.
```

考虑下面的思路：

	读入a, b, c

	交换a与b

	交换b与c

	交换c与a

从理论上来讲，最终的结果应该是：a不动，而b与c交换。

考虑下面的程序：

```delphi
//程序5_1_5_swap
var a, b, c, t :longint;
begin
	read(a, b, c);
	t := a; a := b; b := t;
	t := b; b := c; c := t;
	t := a; a := c; c := t;
	writeln(a); writeln(b); writeln(c);
end.
```

显然这种方法是正确的。然而下面的程序：

```delphi
//程序5_1_6_swap_orz
procedure swap(x, y :longint);
var t :longint;
begin
	t := x; x := y; y := t;
end;
var a, b, c :longint;
begin
	read(a, b, c);
	swap(a, b); swap(b, c); swap(a, c);
	writeln(a); writeln(b); writeln(c);
end.
```

它没有得到正确的结果，因为它没有进行任何交换。

这是因为，调用swap的时候，默认是将变量中存储的数值代入计算（而不是将变量直接带入），这样，所进行的一切交换就只能作用于过程当中，跳出过程以后就会发现原变量没有任何改变。

如果需要强制关联变量（即修改原变量的值），请在<参数变量名>之前加入关键字var，即：

```delphi
procedure swap(var x, y :longint);
```

一定要记住！！！

其实这个就是传说中的形参和变参咯～

---------

gl & hf;

开始新的OI征程吧～

